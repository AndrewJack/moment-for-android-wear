apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion compileAndTargetSdkLevel
    buildToolsVersion buildTools
    publishNonDefault true

    defaultConfig {
        applicationId "technology.mainthread.apps.moment"
        minSdkVersion 20
        targetSdkVersion compileAndTargetSdkLevel
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    productFlavors {
        local {
            applicationId "technology.mainthread.apps.moment.local"
        }

        dev {
            applicationId "technology.mainthread.apps.moment.dev"
        }

        prod {
            applicationId "technology.mainthread.apps.moment"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
        }
        release {
            if (keyProps["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile project(':common')

    // play services
    compile "com.google.android.gms:play-services-wearable:${commonPlayServicesVersion}"

    // Support libraries
    compile "com.google.android.support:wearable:1.4.0"
    compile "com.android.support:recyclerview-v7:${commonSupportLibraryVersion}"
    compile "com.android.support:support-v4:${commonSupportLibraryVersion}"

    // Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "javax.annotation:javax.annotation-api:1.2"
}

android.variantFilter { variant ->
    if(
    (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('local'))
    ) {
        variant.setIgnore(true);
    } else {
        variant.setIgnore(false);
    }
}

apply from: '../config/static-analysis.gradle'
