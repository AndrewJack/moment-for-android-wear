apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion compileAndTargetSdkLevel
    buildToolsVersion buildTools
    publishNonDefault true

    defaultConfig {
        applicationId "technology.mainthread.apps.moment"
        minSdkVersion 20
        targetSdkVersion compileAndTargetSdkLevel
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            def File localPropsFile = file('../local.properties')
            if (localPropsFile.exists()) {
                localProps.load(new FileInputStream(localPropsFile))
            }
            def Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    productFlavors {
        local {
            applicationId "technology.mainthread.apps.moment.local"
        }

        dev {
            applicationId "technology.mainthread.apps.moment.dev"
        }

        prod {
            applicationId "technology.mainthread.apps.moment"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile project(':common')
    // downgraded - 8.1.0 doesn't appear to be pushed out to wearables yet
    compile "com.google.android.gms:play-services-wearable:7.8.0"
    compile "com.google.android.support:wearable:1.3.0"

    // Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "org.glassfish:javax.annotation:10.0-b28"
}

android.variantFilter { variant ->
    if(
    (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('local'))
    ) {
        variant.setIgnore(true);
    } else {
        variant.setIgnore(false);
    }
}